plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id 'io.freefair.lombok' version '5.3.0'
    id 'maven-publish'
}

println """
*******************************************
 You are building SKCraft Launcher! 

 Output files will be in [subproject]/build/libs
*******************************************
"""

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'

    group = 'com.skcraft'
    version = '4.6-SNAPSHOT'

    java {
        toolchain {
            languageVersion.set(JavaLanguageVersion.of(8))
        }
    }

    repositories {
        mavenCentral()
    }

    if (JavaVersion.current().isJava8Compatible()) {
        // Java 8 turns on doclint which we fail
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }

    tasks.withType(JavaExec) {
        workingDir = new File(rootDir, "run/")
        workingDir.mkdirs()
    }

    publishing {
        repositories {
            maven {
                name = "GitHubPackages"
                url = "https://maven.pkg.github.com/Morne-Brume/Launcher"
                credentials {
                    username = System.getenv("GITHUB_ACTOR")
                    password = System.getenv("GITHUB_TOKEN")
                }
            }
        }
        publications {
            gpr(MavenPublication) {
                from(components.java)
            }
        }
    }

    // Work around gradle shadow bug
    // see https://github.com/johnrengelman/shadow/issues/713
    afterEvaluate {
        startScripts {
            dependsOn(shadowJar)
        }

        distTar {
            dependsOn(shadowJar)
        }

        distZip {
            dependsOn(shadowJar)
        }

        startShadowScripts {
            dependsOn(jar)
        }

        shadowDistTar {
            dependsOn(jar)
        }

        shadowDistZip {
            dependsOn(jar)
        }
    }
}

task clean {
    subprojects {
        rootProject.clean.dependsOn tasks.matching { it.name == "clean" }
    }
}
